#Область ПрограммныйИнтерфейс
// Формирование формы документа
//
Процедура ДополнитьФорму(Форма) Экспорт
ИмяФормы = Форма.ИмяФормы;

Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
	ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	ДобавитьГруппуСкидкиСкидкаВГруппаШапкаЛево(Форма);
	ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма);
	ДобавитьКнопкуПересчитатьСкидкуВГруппаШапкаЛево(Форма);
ИначеЕсли ИмяФормы = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда	
	ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);	
ИначеЕсли ИмяФормы = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
	ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма);			
ИначеЕсли ИмяФормы = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	
ИначеЕсли ИмяФормы = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	
КонецЕсли;

КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура  ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	
	ПолеВыбора = Форма.Элементы.Добавить("КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеВыбора.ПутьКДанным = "Объект.КонтактноеЛицо";
	ПолеВыбора.Вид = ВидПоляФормы.ПолеВвода;	 
	
КонецПроцедуры

Процедура  ВставитьПолеКонтактноеЛицоНаФормуПередСуммаДокумента(Форма)
	
	ПолеВыбора = Форма.Элементы.Вставить("КонтактноеЛицо", Тип("ПолеФормы"), , Форма.Элементы.СуммаДокумента);
	ПолеВыбора.ПутьКДанным = "Объект.КонтактноеЛицо";
	ПолеВыбора.Вид = ВидПоляФормы.ПолеВвода;
	
КонецПроцедуры

Процедура  ДобавитьГруппуСкидкиСкидкаВГруппаШапкаЛево(Форма)
	
	Группа = Форма.Элементы.Добавить("ГруппаСкидки", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	
КонецПроцедуры

Процедура  ДобавитьПолеСогласованнаяСкидкаВГруппаШапкаЛево(Форма)
	
	ПолеСкидки = Форма.Элементы.Добавить("СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаСкидки);
	ПолеСкидки.ПутьКДанным = "Объект.СогласованнаяСкидка";
	ПолеСкидки.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСкидки.УстановитьДействие("ПриИзменении", "ДЗ_СогласованнаяСкидкаПриИзменении");
	
КонецПроцедуры

Процедура ДобавитьКнопкуПересчитатьСкидкуВГруппаШапкаЛево(Форма)
	
	Команда = Форма.Команды.Добавить("ДЗ_ПересчитатьСкидку");
	Команда.Заголовок = ("Пересчитать скидку");
	Команда.Действие = "ДЗ_ПересчитатьСкидку";
	Команда.Картинка = БиблиотекаКартинок.Обновить;
	Команда.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	КомандаФормы = Форма.Элементы.Добавить("ДЗ_ПересчитатьСкидку", Тип("КнопкаФормы"), Форма.Элементы.ГруппаСкидки); 
	КомандаФормы.ИмяКоманды = "ДЗ_ПересчитатьСкидку";
	КомандаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 	
	
КонецПроцедуры


#КонецОбласти	